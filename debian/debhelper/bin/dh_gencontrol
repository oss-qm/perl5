#!/usr/bin/perl -w

=head1 NAME

dh_gencontrol - generate and install control file

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_gencontrol> [S<I<debhelper options>>] [S<B<--> I<params>>]

=head1 DESCRIPTION

dh_gencontrol is a debhelper program that is responsible for generating
control files, and installing them into the DEBIAN directory with the
proper permissions.

This program is merely a wrapper around L<dpkg-gencontrol(1)>, which calls
it once for each package being acted on, and passes in some additional
useful flags including "-isp". 

=head1 OPTIONS

=over 4

=item B<-u>I<params>, B<--dpkg-gencontrol-params>I<params>

=item B<--> I<params>

Pass "params" to L<dpkg-gencontrol(1)>.

=back

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $ext=pkgext($package);
	
	my $changelog=pkgfile($package,'changelog');
	if (! $changelog) {
		$changelog='debian/changelog';
	}

	if ( ! -d "$tmp/DEBIAN" ) {
		doit("install","-o",0,"-g",0,"-d","$tmp/DEBIAN");
	}

	# Generate and install control file.
	doit("dpkg-gencontrol","-l$changelog","-isp","-p$package",
		     "-Tdebian/${ext}substvars", "-P$tmp",@{$dh{U_PARAMS}});

	# This chmod is only necessary if the user sets the umask to
	# something odd.
	doit("chmod","644","$tmp/DEBIAN/control");
	
	doit("chown","0.0","$tmp/DEBIAN/control");
}

=head1 SEE ALSO

L<debhelper(1)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut

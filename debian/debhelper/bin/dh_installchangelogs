#!/usr/bin/perl -w

=head1 NAME

dh_installchangelogs - install changelogs into package build directories

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_installchangelogs> [<S<I<debhelper options>>] [B<-k>] [I<upstream>]

=head1 DESCRIPTION

dh_installchangelogs is a debhelper program that is responsible for
installing changelogs into package build directories.

dh_installchangelogs determines if the package is a debian native package,
and if so, it installs debian/changelog into usr/share/doc/package/changelog
in the package build directory. Otherwise, it installs debian/changelog
into usr/share/doc/package/changelog.Debian in the package build directory. (If
files named debian/package.changelog exist, they will be used in preference
to debian/changelog.)

If an upstream changelog file is specified as an option, and the package is
not a native debian package, then this upstream changelog will be installed
as usr/share/doc/package/changelog in the package build directory. If the
changelog is a html file (determined by file extension), it will be
installed as usr/share/doc/package/changelog.html instead, and will be
converted to plain text with html2text to generate
usr/share/doc/package/changelog.

=head1 OPTIONS

=over 4

=item B<-k>, B<--keep>

Keep the original name of the upstream changelog. This will be accomplished
by installing the upstream changelog as "changelog", and making a symlink from
that to the original name of the changelog file. This can be useful if the
upstream changelog has an unusual name, or if other documentation in the
package refers to the changelog file.

=item I<upstream>

Install this file as the upstream changelog.

=back

=head1 NOTES

It is an error to specify an upstream changelog file for a debian native
package.

=cut

init();

my $upstream=shift;

if (isnative($dh{MAINPACKAGE}) && defined $upstream) {
	error("Cannot specify an upstream changelog for a native debian package.");
}

my $changelog_name="changelog.Debian";
if (isnative($dh{MAINPACKAGE})) {
	$changelog_name='changelog';
}

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $changelog=pkgfile($package,"changelog");

	if (!$changelog) {
		$changelog="debian/changelog";
	}

	if (! -e $changelog) {
		error("could not find changelog $changelog");
	}

	if (! -d "$tmp/usr/share/doc/$package") {
		# If it is a dangling symlink, then don't do anything.
		# Think multi-binary packages that depend on each other and
		# want to link doc dirs.
		next if -l "$tmp/usr/share/doc/$package";

		doit("install","-d","$tmp/usr/share/doc/$package");
	}
	doit("install","-o",0,"-g",0,"-p","-m644",$changelog,
		"$tmp/usr/share/doc/$package/$changelog_name");

	if ($upstream) {
		my $link_to;
		if ($upstream=~m/\.html?$/i) {
			# HTML changelog
			doit("install","-o",0,"-g",0,"-p","-m644",
				$upstream,"$tmp/usr/share/doc/$package/changelog.html");
			doit("html2text","-nobs","-o","$tmp/usr/share/doc/$package/changelog",$upstream);
			$link_to='changelog.html';
		}
		else {
			doit("install","-o",0,"-g",0,"-p","-m644",
				$upstream,"$tmp/usr/share/doc/$package/changelog");
			$link_to='changelog';
		}
		if ($dh{K_FLAG}) {
			# Install symlink to original name of the upstream changelog file.
			# Use basename in case original file was in a subdirectory or something.
			doit("ln","-sf",$link_to,"$tmp/usr/share/doc/$package/".basename($upstream));
		}
	}
}

=head1 SEE ALSO

L<debhelper(1)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut

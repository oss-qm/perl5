#!/usr/bin/perl -w

=head1 NAME

dh_installdeb - install files into the DEBIAN directory

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_installdeb> [S<I<debhelper options>>]

=head1 DESCRIPTION

dh_installdeb is a debhelper program that is responsible for installing
files into the DEBIAN directories in package build directories with the
correct permissions.

dh_installdeb automatically installs the following files from debian/ into
the DEBIAN directory:
  package.postinst
  package.preinst
  package.postrm
  package.prerm
  package.shlibs
  package.conffiles

The postinst, preinst, postrm, and prerm are handled specially: If a
corresponding file named debian/script.debhelper exists, the contents of that
file are merged into the script as follows: If the script exists, then
anywhere in it that "#DEBHELPER#" appears, the text of the .debhelper file is
inserted. If the script does not exist, then a script is generated from
the .debhelper file. The .debhelper files are created by other debhelper
programs, such as L<dh_installmenu(1)>, and are shell script fragments.

If DH_COMPAT is set to 3 or higher, all files in the etc/ directory in a
package will automatically be flagged as conffiles by this program, so
there is no need to list them manually in package.conffiles.

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	my $ext=pkgext($package);

	if (! -d "$tmp/DEBIAN") {
		doit("install","-o",0,"-g",0,"-d","$tmp/DEBIAN");
	}

	# Install debian install scripts.
	# If any .debhelper files exist, add them into the scripts.
	foreach my $file (qw{postinst preinst prerm postrm}) {
		my $f=pkgfile($package,$file);
		if ($f) {
			if (-f "debian/$ext$file.debhelper") {
				# Add this into the script, where it has
				# #DEBHELPER#
				# TODO: do internally, no perl fork?
				complex_doit("perl -pe 's~#DEBHELPER#~qx{cat debian/$ext$file.debhelper}~eg' < $f > $tmp/DEBIAN/$file");
		        }
			else {
				# Just get rid of any #DEBHELPER# in the 
				# script.
				complex_doit("sed s/#DEBHELPER#// < $f > $tmp/DEBIAN/$file");
			}
			doit("chown","0.0","$tmp/DEBIAN/$file");
			doit("chmod",755,"$tmp/DEBIAN/$file");
		}
		else {
			# Auto-generate script header and add .debhelper
			# content to it.
			if (-f "debian/$ext$file.debhelper") {
				complex_doit("printf '#!/bin/sh\nset -e\n' > $tmp/DEBIAN/$file");
				complex_doit("cat debian/$ext$file.debhelper >> $tmp/DEBIAN/$file");
				doit("chown","0.0","$tmp/DEBIAN/$file");
				doit("chmod",755,"$tmp/DEBIAN/$file");
			}
		}
	}

	# Install non-executable files
	foreach my $file (qw{shlibs conffiles}) {
		my $f=pkgfile($package,$file);
		if ($f) {
			doit("install","-o",0,"-g",0,"-m",644,"-p",$f,"$tmp/DEBIAN/$file");
		}
	}

	# Automatic conffiles registration: If it is in /etc, it is a
	# conffile.
	if (! compat(2) && -d "$tmp/etc") {
		complex_doit("find $tmp/etc -type f -printf '/etc/%P\n' >> $tmp/DEBIAN/conffiles");
		# Anything found?
		if (-z "$tmp/DEBIAN/conffiles") {
			doit("rm", "-f", "$tmp/DEBIAN/conffiles");
		}
		else {
			doit("chmod", 644, "$tmp/DEBIAN/conffiles");
		}
	}
}

=head1 SEE ALSO

L<debhelper(1)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
